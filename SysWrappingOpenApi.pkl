//===----------------------------------------------------------------------===//
//  Copyright (c) 2025 Javier Cuesta
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//  http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//===----------------------------------------------------------------------===//

extends "@openapi/Document.pkl"

import "@openapi/Components.pkl"
import "@openapi/Schema.pkl"
import "@openapi/HTTPResponse.pkl"

import "VaultApiResponse.pkl"
import "VaultDataResponse.pkl"
import "CommonComponents.pkl"

info {
  title = "Vault Courier System Wrapping API"
  description = "Wrapping endpoints. Vault Courier is a Swift client for Hashicorp Vault and OpenBao"
  version = "0.2.0"
  license {
    name = "Apache License 2.0"
  }
}

servers {
  new { 
    url = "http://127.0.0.1:8200/v1"
    description = "Default http server url"
  }
  new { 
    url = "https://127.0.0.1:8200/v1"
    description = "Default https server url"
  }
}

paths {
  ["/sys/wrapping/wrap"] {
    description = "Wraps the given user-supplied json data inside a response-wrapped token."
    parameters {
      new Reference {
        `$ref` = "#/components/parameters/VaultTokenHeader"
      }
      new Reference {
        `$ref` = "#/components/parameters/WrapTTLHeader"
      }
    }
    post {
      operationId = "wrap"
      tags {
        "wrapping"
      }
      requestBody {
        required = true
        content {
          ["application/json"] {
            schema = new Schema {
              type = "object"
            }
          }
        }
      }
      responses {
        ["200"] {
          description = "OK"
          content {
            ["application/json"] {
              schema = new Reference {
                `$ref` = "#/components/schemas/VaultWrappedResponse"
              }
            }
          }
        }
        ...CommonComponents.responses
      }
    }
  }

  ["/sys/wrapping/rewrap"] {
    description = "Rewraps an existing response-wrapped token. The newly generated token inherits the original token's creation TTL and preserves the same response data. Once rewrapped, the old token is invalidated. This mechanism is useful for securely storing secrets in response-wrapped tokens when periodic rotation is required."
    parameters {
      new Reference {
        `$ref` = "#/components/parameters/VaultTokenHeader"
      }
    }
    post {
      operationId = "rewrap"
      tags {
        "wrapping"
      }
      requestBody {
        required = true
        content {
          ["application/json"] {
            schema = new Schema {
              type = "object"
              properties {
                ["token"] {
                  type = "string"
                }
              }
              required {
                "token"
              }
            }
          }
        }
      }
      responses {
        ["200"] {
          description = "OK"
          content {
            ["application/json"] {
              schema = new Reference {
                `$ref` = "#/components/schemas/VaultWrappedResponse"
              }
            }
          }
        }
        ...CommonComponents.responses
      }
    }
  }

  ["/sys/wrapping/unwrap"] {
    description = "Unwraps a wrapped response"
    parameters {
      new Reference {
        `$ref` = "#/components/parameters/VaultTokenHeader"
      }
    }
    post {
      operationId = "unwrap"
      tags {
        "wrapping"
      }
      requestBody {
        required = true
        content {
          ["application/json"] {
            schema = new Schema {
              type = "object"
              properties {
                ["token"] {
                  type = "string"
                }
              }
            }
          }
        }
      }
      responses {
        ["200"] {
          description = "OK"
          content {
            ["application/json"] {
              schema = new Reference {
                `$ref` = "#/components/schemas/VaultApiResponse"
              }
            }
          }
        }
        ...CommonComponents.responses
      }
    }
  }

  ["/sys/wrapping/lookup"] {
    description = "Looks up wrapping properties for the given token"
    parameters {
      new Reference {
        `$ref` = "#/components/parameters/VaultTokenHeader"
      }
    }
    post {
      operationId = "lookup"
      tags {
        "wrapping"
      }
      requestBody {
        required = true
        content {
          ["application/json"] {
            schema = new Schema {
              type = "object"
              properties {
                ["token"] {
                  type = "string"
                }
              }
            }
          }
        }
      }
      responses {
        ["200"] {
          description = "OK"
          content {
            ["application/json"] {
              schema = (VaultDataResponse) {
                properties  {
                  ["data"] {
                    properties {
                      ["creation_path"] {
                        type = "string"
                      }
                      ["creation_time"] {
                        type = "string"
                      }
                      ["creation_ttl"] {
                        type = "integer"
                      }
                    }
                    required {
                      "creation_path"
                      "creation_time"
                      "creation_ttl"
                    }
                  }
                }
              }
            }
          }
        }
        ...CommonComponents.responses
      }
    }
  }
}

components {
  parameters {
    ...CommonComponents.parameters
  }
  schemas {
    ["VaultApiResponse"] = new VaultApiResponse {}

    ["VaultWrappedResponse"] = (VaultApiResponse) {
      properties {
        ["wrap_info"] {
          properties {
            ["token"] {
              type = "string"
            }
            ["ttl"] {
              type = "integer"
            }
            ["creation_time"] {
              type = "string"
            }
            ["accessor"] {
              type = "string"
            }
            ["creation_path"] {
              type = "string"
            }
            ["wrapped_accessor"] {
              type = "string"
            }
          }
          required {
            "token"
            "accessor"
            "ttl"
            "creation_time"
            "creation_path"
          }
        }
      }
      required {
        "wrap_info"
      }
    }

    ["VaultErrorsResponseBody"] {
      type = "object"
      properties {
        ["errors"] {
          type = "array"
          description = "List of errors"
          items = new Schema {
            type = "string"
          }
        }
      }
    }
  }
}
