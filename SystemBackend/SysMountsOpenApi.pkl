//===----------------------------------------------------------------------===//
//  Copyright (c) 2025 Javier Cuesta
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//  http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//===----------------------------------------------------------------------===//

extends "@openapi/Document.pkl"

import "@openapi/PathItem.pkl"
import "@openapi/Reference.pkl"
import "@openapi/Schema.pkl"

import "@openapi/Components.pkl"

import ".../VaultDataResponse.pkl"
import ".../CommonComponents.pkl"

info {
  title = "Vault Courier System Mounts API"
  description = "System Mounts endpoints. Vault Courier is a Swift client for Hashicorp Vault and OpenBao"
  version = "0.2.0"
  license {
    name = "Apache License 2.0"
  }
}

servers {
  new { 
    url = "{protocol}://127.0.0.1:8200/v1"
    description = "Default server url"
    variables {
      ["protocol"] {
        enum {
          "http"
          "https"
        }
        default = "https"
      }
    }
  }
}

paths {
  // https://developer.hashicorp.com/vault/api-docs/system/mounts
  ["/sys/mounts/{path}"] {
    description = "Mount a new backend at a new path. Example: KV2"
    parameters {
      new {
        name = "path"
        description = "The path to mount to. Example: \"aws/east\""
        `in` = "path"
        schema {
          type = "string"
        }
        required = true
      }
      new Reference {
        `$ref` = "#/components/parameters/VaultTokenHeader"
      }
    }
    get {
      summary = "Get SecretEngine Config"
      description = "Returns the configuration of a specific secret engine"
      operationId = "mounts-read-secrets-engine"
      tags {
        "mounts"
      }
      responses {
        ["200"] {
          description = "OK"
          content {
            ["application/json"] {
              schema = new Reference {
                `$ref` = "#/components/schemas/SecretsEngineConfigResponse"
              }
            }
          }
        }
      }
    }
    post {
      summary = "Enable Secret Engine"
      description = "Enable a new secrets engine at the given path."
      operationId = "mounts-enable-secrets-engine"
      tags {
        "mounts"
      }
      requestBody {
        required = true
        content {
          ["application/json"] {
            schema = new Reference {
              `$ref` = "#/components/schemas/MountsEnableSecretsEngineRequest"
            }
          }
        }
      }
      responses {
        ["204"] {
          description = "NoContent"
        }
      }
    }
    `delete` {
      summary = "Disable Secret Engine"
      description = "disables the mount point specified in the URL"
      operationId = "mounts-disable-secrets-engine"
      tags {
        "mounts"
      }
      responses {
        ["204"] {
          description = "NoContent"
        }
      }
    }
  }
}

components {
  parameters {
    ...CommonComponents.parameters
  }

  schemas {
    ...mountComponents.schemas

    ["VaultErrorsResponseBody"] {
      type = "object"
      properties {
        ["errors"] {
          type = "array"
          description = "List of errors"
          items = new Schema {
            type = "string"
          }
        }
      }
    }
  }
}

hidden mountComponents: Components = new {
  schemas {
    ["MountsEnableSecretsEngineRequest"] {
      type = "object"
      properties {
          ["config"] {
            type = "object"
            description = "Configuration for this mount, such as `default_lease_ttl` and `max_lease_ttl`."
            format = "map"
          }
          ["description"] {
            type = "string"
            description = "User-friendly description for this mount."
          }
          ["external_entropy_access"] {
            type = "boolean"
            description = "Whether to give the mount access to Vault's external entropy."
            default = false
          }
          ["local"] {
            type = "boolean"
            description = "Mark the mount as a local mount, which is not replicated and is unaffected by replication."
            default = false
          }
          ["options"] {
            type = "object"
            description = "The options to pass into the backend. Should be a json object with string keys and values."
            format = "kvpairs"
          }
          ["plugin_name"] {
            type = "string"
            description = "Name of the plugin to mount based from the name registered in the plugin catalog."
          }
          ["plugin_version"] {
            type = "string"
            description = "The semantic version of the plugin to use, or image tag if `oci_image` is provided."
          }
          ["seal_wrap"] {
            type = "boolean"
            description = "Whether to turn on seal wrapping for the mount."
            default = false
          }
          ["type"] {
            type = "string"
            description = "The type of the backend. Example: \"passthrough\""
          }
      }
    }

    ["SecretsEngineConfigResponse"] = (VaultDataResponse) {
      properties {
        ["data"] {
          properties {
            ["config"] = mountConfig
            ["accessor"] {
              type = "string"
            }
            ["description"] {
              type = "string"
              description = "User-friendly description for this mount."
            }
            ["external_entropy_access"] {
              type = "boolean"
              description = "Whether to give the mount access to Vault's external entropy."
              default = false
            }
            ["local"] {
              type = "boolean"
              description = "Mark the mount as a local mount, which is not replicated and is unaffected by replication."
              default = false
            }
            ["options"] {
              type = "object"
              description = "The options to pass into the backend. Should be a json object with string keys and values."
              format = "kvpairs"
            }
            ["plugin_version"] {
              type = "string"
              description = "The semantic version of the plugin to use, or image tag if `oci_image` is provided."
            }
            ["running_plugin_version"] {
              type = "string"
            }
            ["seal_wrap"] {
              type = "boolean"
              description = "Whether to turn on seal wrapping for the mount."
              default = false
            }
            ["type"] {
              type = "string"
              description = "The type of the backend. Example: \"passthrough\""
            }
          }
        }
      }
    }
  }
}

// https://swiftpackageindex.com/apple/swift-openapi-generator/1.10.2/documentation/swift-openapi-generator/useful-openapi-patterns#oneOfs
hidden mountConfig: Schema.PropertySchema = new {
  anyOf {
    new Schema {
      oneOf {
        new Schema {
          type = "object"
          properties {
            ["default_lease_ttl"] {
              type = "integer"
            }
            ["force_no_cache"] {
              type = "boolean"
            }
            ["max_lease_ttl"] {
              type = "integer"
            }
          }
          required {
            "default_lease_ttl"
            "force_no_cache"
            "max_lease_ttl"
          }
        }
        new Schema {
          type = "object"
          format = "map"
        }
      }
    }
    new Schema {
      type = "object"
    }
  }
}