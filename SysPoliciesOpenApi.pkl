//===----------------------------------------------------------------------===//
//  Copyright (c) 2025 Javier Cuesta
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//  http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//===----------------------------------------------------------------------===//

extends "@openapi/Document.pkl"

import "@openapi/PathItem.pkl"
import "@openapi/Reference.pkl"
import "@openapi/Schema.pkl"
import "@openapi/Components.pkl"

import "VaultDataResponse.pkl"
import "CommonComponents.pkl"

info {
  title = "Vault Courier System Policies API"
  description = "System Policies endpoints. Vault Courier is a Swift client for Hashicorp Vault and OpenBao"
  version = "0.2.0"
  license {
    name = "Apache License 2.0"
  }
}

servers {
  new { 
    url = "http://127.0.0.1:8200/v1"
    description = "Default http server url"
  }
  new { 
    url = "https://127.0.0.1:8200/v1"
    description = "Default https server url"
  }
}

paths {
  ["/sys/policies/acl/{name}"] {
    description = "Read, Modify, or Delete an access control policy."
    parameters {
      new {
        name = "name"
        description = "The name of the policy. Example: \"ops\""
        `in` = "path"
        schema {
          type = "string"
        }
        required = true
      }
      new Reference {
        `$ref` = "#/components/parameters/VaultTokenHeader"
      }
    }
    get {
      summary = "Read ACL policy."
      description = "Retrieves information about the named ACL policy"
      operationId = "policies-read-acl-policy"
      tags {
        "policies"
      }
      responses {
        ["200"] {
          description = "OK"
          content {
            ["application/json"] {
              schema = new Reference {
                `$ref` = "#/components/schemas/ReadACLPolicyResponse"
              }
            }
          }
        }
        ...CommonComponents.responses
      }
    }
    post {
      summary = "Add/update ACL policy."
      description = "Add a new or update an existing ACL policy."
      operationId = "policies-write-acl-policy"
      tags {
        "policies"
      }
      requestBody {
        required = true
        content {
          ["application/json"] {
            schema = new Reference {
              `$ref` = "#/components/schemas/PoliciesWriteAclPolicyRequest"
            }
          }
        }
      }
      responses {
        ["204"] {
          description = "NoContent"
        }
        ...CommonComponents.responses
      }
    }
    `delete` {
      summary = "Delete Policy"
      description = "Deletes the ACL policy with the given name. This will immediately affect all users associated with this policy. (A deleted policy set on a token acts as an empty policy.)"
      operationId = "policies-delete-acl-policy"
      tags {
        "policies"
      }
      responses {
        ["204"] {
          description = "NoContent"
        }
        ...CommonComponents.responses
      }
    }
  }
}

components {
  parameters {
    ...CommonComponents.parameters
  }

  schemas {
    ...policyComponents.schemas

    ["VaultErrorsResponseBody"] {
      type = "object"
      properties {
        ["errors"] {
          type = "array"
          description = "List of errors"
          items = new Schema {
            type = "string"
          }
        }
      }
    }
  }
}

hidden policyComponents: Components = new {
  schemas {
    ["PoliciesWriteAclPolicyRequest"] {
      type = "object"
      properties {
        ["policy"] {
          type = "string"
          description = "The rules of the policy."
        }
      }
    }

    ["ReadACLPolicyResponse"] = (VaultDataResponse) {
      properties {
        ["data"] {
          properties {
            ["name"] {
              type = "string"
              description = "The name of the policy."
            }
            ["policy"] {
              type = "string"
              description = "The rules of the policy."
            }
          }
          required {
            "name"
            "policy"
          }
        }
      }
    }
  }
}